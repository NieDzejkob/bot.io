use crate::ast::*;
use num_bigint::BigUint;

grammar;

Boxed<T>: Box<T> = {
    T => Box::new(<>)
};

Spanned<T>: Span<T> = {
    <l:@L> <v:T> <r:@R> => Span(v, (l, r))
};

Tier<Op, NextTier>: Expr<'input> = {
    Boxed<Tier<Op, NextTier>> Op Boxed<NextTier> => Expr::BinOp(<>),
    NextTier
};

pub Command: Command<'input> = {
    Expr => Command::Expr(<>),
    Pred => Command::Pred(<>),
};

Pred: Pred<'input> = {
    Expr Cmp Expr => Pred::Cmp(<>)
};

Cmp: Cmp = {
    "=" => Cmp::Eq,
    "<" => Cmp::Lt,
    "<=" => Cmp::Le,
    ">" => Cmp::Gt,
    ">=" => Cmp::Ge,
};

pub Expr = {
    Product,
    "if" <Boxed<Pred>> "then" <Boxed<Expr>> "else" <Boxed<Expr>> => Expr::If(<>),
};

Product = Tier<ProductOp, Factor>;
Factor = Tier<FactorOp, Neg>;

ProductOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

FactorOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
    "%" => BinOp::Mod,
};

Neg: Expr<'input> = {
    "-" <Boxed<Term>> => Expr::Neg(<>),
    Term,
};

Term: Expr<'input> = {
    "(" <Expr> ")",
    Num => Expr::Num(<>),
    Spanned<Ident> => Expr::Ident(<>),
    <id:Spanned<Ident>> "(" <mut params:(<Expr> ",")*> <last:Expr> ")" => {
        params.push(last);
        Expr::Func(id, params)
    }
};

Num: BigUint = r"0|[1-9][0-9]*" => <>.parse().unwrap();
Ident: &'input str = r"[A-Za-z_][A-Za-z_0-9]*";
